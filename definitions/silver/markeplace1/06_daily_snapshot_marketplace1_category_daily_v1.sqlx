config { 
    type: "table",
    partitionBy: "snapshot_date",
    clusterBy: ["asin"],
    tags: ["silver", "marketplace1", "daily_snapshot"]
}

WITH listing_latest AS (
  SELECT * EXCEPT(rn)
  FROM (
    SELECT
      l.*,
      ROW_NUMBER() OVER (PARTITION BY asin ORDER BY extracted_at DESC) AS rn
    FROM ${ref("01_refresh_marketplace1_product_listing_current")} l
    WHERE asin IS NOT NULL AND TRIM(asin) != ''
  )
  WHERE rn = 1
)
SELECT
  s.snapshot_date,
  s.extracted_at,
  s.asin,
  s.node,
  s.category_label,
  s.currency,

  -- PDP truth
  s.pdp_price            AS pdp_price,
  s.pdp_price_shipping   AS pdp_price_shipping,
  s.pdp_total_price      AS pdp_total_price,

  s.buybox_seller_id,
  s.buybox_seller_name,
  s.buybox_is_amazon,
  s.buybox_stock,
  s.pdp_extraction_state,

  -- Listing context (latest row per ASIN; ignore extracted_at mismatch)
  l.is_sponsored,
  l.position AS listing_position,

  -- Use listing-provided page (no inferred bucket logic)
  SAFE_CAST(l.page_number AS INT64) AS listing_page,

  -- Offers summary (daily)
  COALESCE(o.offer_rows, 0) AS offer_rows,
  COALESCE(o.seller_id_count, 0) AS seller_id_count,
  o.min_total_price,
  o.median_total_price,
  o.max_total_price,

  -- Coverage flag for trust
  CASE
    WHEN s.pdp_extraction_state = 'COMPLETE' AND COALESCE(o.offer_rows, 0) > 0 THEN 'DETAIL_PLUS_OFFERS'
    WHEN s.pdp_extraction_state = 'COMPLETE' THEN 'DETAIL_ONLY'
    ELSE 'INCOMPLETE_PDP'
  END AS data_coverage

FROM ${ref("04_marketplace1_snapshot_category_daily_v0")} s
LEFT JOIN listing_latest l
  ON l.asin = s.asin
LEFT JOIN ${ref("05_daily_offers_summary_marketplace1_daily_v0")} o
  ON o.asin = s.asin
 AND o.offer_date = s.snapshot_date
