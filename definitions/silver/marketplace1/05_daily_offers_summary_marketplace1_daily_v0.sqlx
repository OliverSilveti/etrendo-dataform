config { 
    type: "table",
    partitionBy: "offer_date",
    clusterBy: ["asin"],
    tags: ["silver", "marketplace1", "daily_offers"]
}

SELECT
  DATE(extracted_at) AS offer_date,
  asin,

  COUNT(*) AS offer_rows,
  COUNT(DISTINCT NULLIF(TRIM(seller_id), '')) AS seller_id_count,

  MIN(SAFE_CAST(price AS NUMERIC) + COALESCE(SAFE_CAST(price_shipping AS NUMERIC), 0)) AS min_total_price,
  APPROX_QUANTILES(SAFE_CAST(price AS NUMERIC) + COALESCE(SAFE_CAST(price_shipping AS NUMERIC), 0), 2)[OFFSET(1)] AS median_total_price,
  MAX(SAFE_CAST(price AS NUMERIC) + COALESCE(SAFE_CAST(price_shipping AS NUMERIC), 0)) AS max_total_price

FROM ${ref("01_marketplace1_price_listing_flat")}
WHERE asin IS NOT NULL AND TRIM(asin) != ''
  AND SAFE_CAST(price AS NUMERIC) > 0   -- filters junk rows
GROUP BY offer_date, asin
