config {
  type: "incremental",
  uniqueKey: ["category_label", "node", "asin"],
  tags: ["silver", "marketplace1", "products"]
}

WITH cleaned AS (
  SELECT
    category_label,
    SAFE_CAST(node AS INT64) AS node,
    asin,
    extracted_at,
    is_sponsored,
    ARRAY_TO_STRING(delivery, " | ") AS delivery,
    SAFE_CAST(extracted_price AS NUMERIC) AS extracted_price,
    price_raw,
    bought_last_month,
    SAFE_CAST(reviews AS INT64) AS reviews,
    NULLIF(TRIM(title), "") AS title,
    link,
    SAFE_CAST(position AS INT64) AS position,
    SAFE_CAST(rating AS FLOAT64) AS rating,
    currency,
    SAFE_CAST(page_number AS INT64) AS page_number
  FROM ${ref("amazon_product_listing_coffee_machines")}
  WHERE asin IS NOT NULL AND asin != ""
),

chosen AS (
  SELECT * EXCEPT (rn)
  FROM (
    SELECT
      cleaned.*,
      ROW_NUMBER() OVER (
        PARTITION BY category_label, node, asin
        ORDER BY extracted_at DESC, is_sponsored DESC
      ) AS rn
    FROM cleaned
  )
  WHERE rn = 1
),

incoming AS (
  SELECT
    category_label,
    node,
    asin,

    extracted_at,
    is_sponsored,

    delivery,
    extracted_price,
    price_raw,
    bought_last_month,
    reviews,
    title,
    link,
    position,
    rating,
    currency,
    page_number,

    extracted_at AS last_seen_at,

    TRUE AS is_active,

    CURRENT_TIMESTAMP() AS silver_updated_at
  FROM chosen
)

SELECT * FROM incoming

