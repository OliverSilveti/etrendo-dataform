config { 
    type: "incremental",
    uniqueKey: ["category_label", "product_key"],
    tags: ["silver", "marketplace2", "products"]
}

WITH cleaned AS (
SELECT
    category_label,

    REGEXP_REPLACE(TRIM(link), r'\\?.*$', '') AS canonical_link,

    CAST(
    ABS(
        FARM_FINGERPRINT(
        REGEXP_REPLACE(TRIM(link), r'\\?.*$', '')
        )
    ) AS STRING
    ) AS product_key,

    NULLIF(TRIM(title), "") AS title,
    price_raw,

    SAFE_CAST(
    REPLACE(
        REPLACE(REGEXP_EXTRACT(price_raw, r'[\\d\\.,]+'), '.', ''),
        ',', '.'
    ) AS NUMERIC
    ) AS price,

    SAFE_CAST(page_number AS INT64) AS page_number,
    extracted_at

FROM ${source("otto_product_listing")}
WHERE link IS NOT NULL
    AND TRIM(link) != ""
),

-- Choose 1 row per product_key
chosen AS (
SELECT * EXCEPT(rn)
FROM (
    SELECT
    cleaned.*,
    ROW_NUMBER() OVER (
        PARTITION BY category_label, product_key
        ORDER BY
        page_number IS NOT NULL DESC,
        extracted_at DESC
    ) AS rn
    FROM cleaned
)
WHERE rn = 1
)

SELECT
category_label,
product_key,
canonical_link,
title,
price_raw,
price,
page_number,
extracted_at,
extracted_at AS last_seen_at,
CURRENT_TIMESTAMP() AS silver_updated_at
FROM chosen
